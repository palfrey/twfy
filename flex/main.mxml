<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml"
    layout="vertical" verticalAlign="middle"
    backgroundColor="white"
    initialize="init();">
    <!-- creationComplete="creationComplete();" -->

    <mx:Style source="style.css"/>

    <mx:Script><![CDATA[

import mx.events.MetadataEvent;
import flash.events.TimerEvent;
import flash.utils.Timer;
import mx.managers.PopUpManager;
import flash.external.*;
//import mx.controls.Alert;

private var hostname:String = 'sandwich.ukcod.org.uk:81';

public function currentTime():int {
    return int(video.playheadTime);
}

public function moveVideoExt(moveGID:String):int {
    for (var i:int = 0; i < allStamps.length; i++) {
        var stamp:Object = allStamps[i];
        if (stamp.gid == moveGID) {
            if (fileID != stamp.file) {
                debugS("Changing video to " + stamp.file + " " + stamp.time);
                change_video(stamp.file, stamp.time);
                video_play(); // Otherwise the old video continues to play...
            } else {
                seek(stamp.time, true, false);
            }
            return 1;
        }
    }
    return 0;
}

[Bindable]
public var videoSource:String;
[Bindable]
public var gid:String;
[Bindable]
public var fileID:String;
[Bindable]
public var stamping:Boolean = false; // If we allow timestamping or just for viewing

private var startAt:int; // If we don't want to start at the beginning of the video
private var playing:Boolean = false; // Whether the video is playing (keep true when video is paused due to slider dragging)

private function change_video(id:String, time:int):void {
    startAt = time;
    fileID = id;
    videoSource = 'http://' + hostname + '/' + fileID + '.flv';
}

private function init():void {
    gid = Application.application.parameters.gid;
    stamping = Application.application.parameters.stamping;
    change_video(Application.application.parameters.file, int(Application.application.parameters.start));
    
    video.addEventListener('metadataReceived', video_onMetaData);
    ExternalInterface.addCallback('currentTime', currentTime);
    ExternalInterface.addCallback('moveVideo', moveVideoExt);
    if (!stamping) {
        stampsSubmitter.send();
    }
}

private var allStamps:Array = new Array();

private function stampsArrived():void {
    allStamps = stampsSubmitter.lastResult.stamps.stamp.toArray();
    //for each (var stamp:Object in stamps) {
    //    allStamps.push(stamp);
    //}
    debugS("Stamps arrived: " + allStamps.length);
}

public var times:Array = [];
public var filepositions:Array = [];
private var firstLoad:Boolean = true;

[Bindable]
public var _duration:int = 0;

private function video_onMetaData(evt:MetadataEvent):void {
    _duration = evt.info.duration;
    debugS("Duration: " + _duration);
    times = evt.info.keyframes.times;
    filepositions = evt.info.keyframes.filepositions;
    if (times && filepositions && startAt>0 && startAt<_duration) {
        playing = true;
        if (firstLoad)
            video.volume = 0;
        seek(startAt, true, firstLoad);
        firstLoad = false;
    }
}

private var liveGID:String = '';

private function video_playheadUpdate():void {
    //if (video.playheadTime > 16770) {
    //   stopButton_click();
    //   Alert.show("Sorry, there is currently a bug going past 4:39:36 on any video.");
    //   return;
    //}
    var pTime:Date = new Date(video.playheadTime * 1000 || 100);
    var tTime:Date = new Date(_duration * 1000);
    time.text = dateFormatter.format(pTime) + " / " + dateFormatter.format(tTime);
    if (!stamping) {
        for (var i:int = 0; i < allStamps.length-1; i++) {
            var stamp:Object = allStamps[i];
            var stampNext:Object = allStamps[i+1];
            if (stamp.gid != liveGID && stamp.file == fileID && video.playheadTime >= stamp.time && video.playheadTime < stampNext.time) {
                ExternalInterface.call('updateSpeech', stamp.gid);
                liveGID = stamp.gid;
                break;
            }
        }
        if (stampNext.gid != liveGID && stampNext.file == fileID && video.playheadTime >= stampNext.time) {
            ExternalInterface.call('updateSpeech', stampNext.gid);
            liveGID = stampNext.gid;
        }
    }
}

private function video_play():void {
    go.label = 'Now!';
    go.styleName = 'nowStyle';
    video.play();
    playing = true;
}

private function video_pause():void {
    reset_big_button();
    video.pause();
    playing = false;
}

private function playPauseButton_click():void {
    if (video.playing) {
        video_pause();
    } else {
        video_play();
    }
}

private function reset_big_button():void {
    go.label = 'Play';
    go.styleName = 'goStyle';
}

private function stopButton_click():void {
    playing = false;
    video.stop();
    if (startAt>0 && startAt<_duration) {
        seek(startAt, false, false);
    } else {
        videoSource = 'http://' + hostname + '/' + fileID + '.flv?start=0';
    }
    reset_big_button();
}

private var thanksWin:Thanks;
private function goButton_click():void {
    if (video.playing) {
        video_pause();
        debugS('Clicked at ' + String(int(video.playheadTime)) + 's');
        resultSubmitter.send();
        thanksWin = PopUpManager.createPopUp(this, Thanks, true) as Thanks;
        thanksWin['thanksNextButton'].addEventListener('click', submit_next);
        thanksWin['thanksRandomButton'].addEventListener('click', submit_random);
        thanksWin['thanksOopsButton'].addEventListener('click', submit_oops);
        PopUpManager.centerPopUp(thanksWin);
        go.visible = false;
    } else {
        video_play();
    }
}

private function submit_next(evt:MouseEvent):void {
    var url:String = '/video/next.php?gid=' + gid + '&file=' + fileID + '&time=' + int(video.playheadTime) + '&action=next';
    debugS("Moving to " + url)
    var req:URLRequest = new URLRequest(url);
    navigateToURL(req, '_self');
}
private function submit_random(evt:MouseEvent):void {
    var url:String = '/video/next.php?action=random';
    var pid:int = int(Application.application.parameters.pid);
    if (pid > 0) url += '&pid=' + pid;
    debugS("Moving to " + url)
    var req:URLRequest = new URLRequest(url);
    navigateToURL(req, '_self');
}

private function submit_oops(evt:MouseEvent):void {
    debugS("Removing just added...");
    oopsSubmitter.send();
    go.visible = true;
    PopUpManager.removePopUp(thanksWin);
}

private function nearest_keyframe(s:int):int {
    for (var i:int = 0; i < times.length; i++) {
        var j:int = i + 1;
        if ((times[i] <= s) && (times[j] >= s)) {
            return i;
        }
    }
    return 0;
}

private function scrubToolTip(val:String):String {
    var pTime:Date = new Date(int(val) * 1000 || 100);
    return dateFormatter.format(pTime);
}

private function video_thumbRelease():void {
    seek(scrubBar.value, true, false);
}

private function seekBack_click(offset:int):void {
    seekBack.enabled = false;
    seekBackSm.enabled = false;
    debugS("Seeking back to " + String(video.playheadTime-offset));
    if (video.playheadTime < offset) {
        change_video(String(int(fileID)-1), 0);
        video_play();
        return;
    }
    seek(video.playheadTime - offset, false, false);
    var alarmTimer:Timer = new Timer(1000,1);
    alarmTimer.addEventListener(TimerEvent.TIMER, onSeekAlarm);
    alarmTimer.start();
}

private function seekFwd_click(offset:int):void {
    seekFwd.enabled = false;
    seekFwdSm.enabled = false;
    if (video.playheadTime + offset > _duration) {
        change_video(String(int(fileID)+1), 0);
        video_play();
        return;
    }
    seek(video.playheadTime + offset, false, false);
    var alarmTimer:Timer = new Timer(1000,1);
    alarmTimer.addEventListener(TimerEvent.TIMER, onSeekAlarm);
    alarmTimer.start();
}

private function onSeekAlarm(evt:TimerEvent):void {
    seekBack.enabled = true;
    seekBackSm.enabled = true;
    seekFwd.enabled = true;
    seekFwdSm.enabled = true;
}

private var _beginning:int = 0;

private function seek(s:int, startImmediately:Boolean, veryFirstLoad:Boolean):void {
    //if (s >= 16770) {
    //    stopButton_click();
    //    Alert.show("Sorry, there is currently a bug going past 4:39:36 on any video.");
    //    return;
    //}
    if (s >= _beginning && s < _beginning + Math.floor((_duration - _beginning) * (video.bytesLoaded/video.bytesTotal)) - 1) {
        video.playheadTime = s;
    } else if (times && filepositions) {
        var pos:int = nearest_keyframe(s);
        debugS("Moving to " + s);
        videoSource = 'http://' + hostname + '/' + fileID + '.flv?start=' + filepositions[pos];
        _beginning = times[pos];
    }
    if (veryFirstLoad) {
        var alarmTimer:Timer = new Timer(500,1);
        alarmTimer.addEventListener(TimerEvent.TIMER, onFirstAlarm);
        alarmTimer.start();
    }
    if (startImmediately || playing) {
        video_play();
    }
}

private function onFirstAlarm(evt:TimerEvent):void {
    video_pause();
    video.volume = 0.75;
}

// Roll-over pretty controls
private function showControls():void { fadeIn.play([controls, volumeBox]); }
private function hideControls():void { fadeOut.play([controls, volumeBox]); }

// Utility functions
private function debugS(s:String):void {
    //debug.text += s + "\n";
}

]]></mx:Script>

    <mx:Fade id="fadeIn" alphaFrom="0.0" alphaTo="1.0" />
    <mx:Fade id="fadeOut" alphaFrom="1.0" alphaTo="0.0" />
    <mx:DateFormatter id="dateFormatter" formatString="JJ:NN:SS" />

    <mx:HTTPService id="resultSubmitter" url="submit.php" method="GET" showBusyCursor="true">
        <mx:request xmlns="">
	    <gid>{gid}</gid>
	    <file>{fileID}</file>
	    <time>{video.playheadTime}</time>
	</mx:request>
    </mx:HTTPService>
    <mx:HTTPService id="oopsSubmitter" url="delete.php" method="GET" showBusyCursor="true">
        <mx:request xmlns="">
	    <gid>{gid}</gid>
	    <oops>{resultSubmitter.lastResult.id}</oops>
	</mx:request>
    </mx:HTTPService>
    <mx:HTTPService id="stampsSubmitter" url="/video/stamps.php" method="GET" result="stampsArrived()">
        <mx:request xmlns="">
	    <file>{fileID}</file>
	    <gid>{gid}</gid>
	</mx:request>
    </mx:HTTPService>
    
        <mx:Canvas rollOver="showControls()" rollOut="hideControls()">
            <mx:VideoDisplay id="video" autoPlay="false" width="320" height="180" source="{videoSource}" volume="{volumeBar.value}"
                playheadUpdate="video_playheadUpdate()" />
	    <mx:VBox id="controls" styleName="controllerStyle" alpha="0.0">
            <!-- <mx:HBox styleName="controlHboxStyle">
            </mx:HBox> -->
        <mx:HBox width="300" styleName="controlHboxStyle">
	        <mx:HSlider id="scrubBar" width="100%" minimum="0" maximum="{_duration}"
                    value="{video.playheadTime}"
		    tickInterval="600" dataTipFormatFunction="scrubToolTip"
                    thumbPress="video.pause();" thumbRelease="video_thumbRelease();"/>
            <!-- <mx:Button styleName="playPauseStyle" toggle="true" selected="{video.playing}"
                click="playPauseButton_click()" />
            <mx:Button styleName="stopStyle" click="stopButton_click()" /> -->
            <!-- <mx:Spacer width="100%" /> -->
            <mx:Label id="time" styleName="timeWStyle" />
        </mx:HBox>
	</mx:VBox>

        <mx:HBox id="volumeBox" styleName="volumeStyle" alpha="0.0">
		<mx:Label styleName="timeWStyle" text="Volume:"/>
	        <mx:HSlider id="volumeBar" width="120" minimum="0" maximum="1"
                    showDataTip="false" liveDragging="true" value="0.75"/>
        </mx:HBox>

        </mx:Canvas>

        <mx:HBox width="300" verticalAlign="middle" horizontalGap="4">
            <mx:Button styleName="playPauseStyle" toggle="true" selected="{video.playing}"
                click="playPauseButton_click()" />
            <mx:Button styleName="stopStyle" click="stopButton_click()" />
	    <mx:Button id="seekBack" toolTip="Back 30s" styleName="seekBackStyle" click="seekBack_click(30)" />
	    <mx:Button id="seekBackSm" toolTip="Back c. 15s" styleName="seekBackSmStyle" click="seekBack_click(11)" />
	    <mx:Button id="seekFwdSm" toolTip="Forward 10s" styleName="seekFwdSmStyle" click="seekFwd_click(10)" />
	    <mx:Button id="seekFwd" toolTip="Forward 30s" styleName="seekFwdStyle" click="seekFwd_click(30)" />
            <!-- <mx:Label id="time" styleName="timeStyle" /> -->
            <!-- <mx:Button label="Play" toggle="true" selected="{video.playing}"
                click="playPauseButton_click()" />
            <mx:Button label="Rewind" click="stopButton_click()" /> -->
            <mx:Spacer width="100%" />
            <mx:Button id="go" label="Play" styleName="goStyle" click="goButton_click()" visible="{stamping}"/>
            <!-- <mx:Image source="@Embed(source='assets/mysociety_sm.gif')" visible="{!stamping}" width="100" includeInLayout="false" /> -->
        </mx:HBox>


<!--        <mx:ProgressBar width="100%" source="{video}"/> -->

	<!-- <mx:TextArea id="debug" width="320" height="200" />
	-->
    
</mx:Application>
