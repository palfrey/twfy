#!/usr/bin/perl -w
#
# handlemail:
# Handle an individual incoming mail message.
#
# This script should be invoked through the .forward mechanism. It processes
# replies to non-reply emails and auto-replies accordingly. Could deal with
# bounces at some point too.
#
# Copyright (c) 2008 UK Citizens Online Democracy. All rights reserved.
# Email: matthew@mysociety.org; WWW: http://www.mysociety.org/
#

my $rcsid = ''; $rcsid .= '$Id: handlemail,v 1.10 2009-05-11 13:37:57 louise Exp $';

use strict;
require 5.8.0;

# Horrible boilerplate to set up appropriate library paths.
use FindBin;
use lib "$FindBin::Bin";
use lib "$FindBin::Bin/../perllib";
use lib "$FindBin::Bin/../../perllib";

use Email::LocalDelivery;

use mySociety::Config;
BEGIN {
    mySociety::Config::set_file("$FindBin::Bin/../conf/general");
}
use mySociety::Email;
use mySociety::EmailUtil;
use mySociety::HandleMail;
use mySociety::SystemMisc;
use TWFYMail;

# Don't print diagnostics to standard error, as this can result in bounce
# messages being generated (only in response to non-bounce input, obviously).
mySociety::SystemMisc::log_to_stderr(0);

my %data = mySociety::HandleMail::get_message();

if ($data{is_bounce_message}) {
    my $bounce_recipient = mySociety::HandleMail::get_bounce_recipient($data{message});
    my $token = mySociety::HandleMail::get_token($bounce_recipient,
        'twfy', mySociety::Config::get('EMAILDOMAIN')
    );
    exit(0) unless $token ;
    exit(0) if $token eq '-DO-NOT-REPLY'; # A bounce we don't care about

    if ($bounce_recipient){
        # try and get the address if the mail was sent with VERP
        my $bounced_address = TWFYMail::get_bounced_address($bounce_recipient);
        TWFYMail::handle_bounce(\%data, $bounced_address);
    }

    exit(0); 
}

# Not a bounce, send an automatic response
my $template = 'reply-autoresponse';
open FP, "$FindBin::Bin/../www/includes/easyparliament/templates/emails/$template" or exit 75;
$template = join('', <FP>);
close FP;

# We generate this as a bounce.
my $mail = mySociety::Email::construct_email({
    Sender => '<>',
    From => [ mySociety::Config::get('CONTACTEMAIL'), 'TheyWorkForYou' ],
    To => $data{return_path},
    _template_ => $template,
    _parameters_ => { },
});

if (mySociety::EmailUtil::EMAIL_SUCCESS
        != mySociety::EmailUtil::send_email($mail, '<>', $data{return_path})) {
    exit(75);
}

exit(0);

